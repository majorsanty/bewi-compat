plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = "FabricMC"
		url = "https://maven.fabricmc.net/"
	}
	maven {
		name = "Sonatype"
		url 'https://oss.sonatype.org/content/repositories/snapshots'
	}
	maven {
		name = "Jamies"
		url "https://maven.jamieswhiteshirt.com/libs-release/"
		content {
			includeGroup "com.jamieswhiteshirt"
		}
	}
	maven {
		name = "Jared"
		url 'https://maven.blamejared.com'
		content {
			includeGroup "vazkii.patchouli"
		}
	}
	maven {
		name = "Ladysnake Libs"
		url = 'https://ladysnake.jfrog.io/artifactory/mods'
		content {
			includeGroup "io.github.ladysnake"
			includeGroupByRegex "io\\.github\\.onyxstudios.*"
			includeGroupByRegex "dev\\.onyxstudios.*"
		}
	}
	maven {
		name = "Cotton"
		url = "https://server.bbkr.space/artifactory/libs-release/"
		content {
			includeGroup "io.github.cottonmc"
		}
	}
	maven {
		name = "Shedaniel"
		url = "https://maven.shedaniel.me"
		content {
			includeGroupByRegex "dev\\.architectury.*"
			includeGroupByRegex "me\\.shedaniel.*"
			includeGroupByRegex "me\\.sargunvohra.*"
		}
	}
	maven {
		name = "Terraformers"
		url = "https://maven.terraformersmc.com/releases/"
		content {
			includeGroup "io.github.prospector"
			includeGroup "com.terraformersmc"
		}
	}
	maven {
		name = "cafeteria.dev"
		url = 'https://maven.cafeteria.dev'
		content {
			includeGroup 'net.adriantodt.fabricmc'
		}
	}
	maven {
		name = "Flemmli97"
		url "https://gitlab.com/api/v4/projects/21830712/packages/maven"
		content {
			includeGroup 'io.github.flemmli97'
		}
	}
	maven {
		name = "Gunpowder"
		url = "https://maven.martmists.com/releases"
		content {
			includeGroup 'io.github.gunpowder'
		}
	}
	maven {
		name = "Permissions"
		url = "https://maven.nucleoid.xyz"
		content {
			includeGroup 'eu.pb4'
		}
	}
	maven {
		name = "CurseMaven"
		url "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		name = "JitPack"
		url 'https://jitpack.io'
	}

	mavenLocal()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Satin
	modImplementation include("io.github.ladysnake:satin:${project.satin_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	// Reach Entity Attributes
	modImplementation include("com.jamieswhiteshirt:reach-entity-attributes:${project.rea_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	// Patchouli
	modImplementation include("vazkii.patchouli:Patchouli:${project.patchouli_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	// Cloth Config
	modImplementation include("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	// CCA
	modApi include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cc_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	modApi include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cc_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	// Fabric Permissions API
	modImplementation include("me.lucko:fabric-permissions-api:${project.fpa_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	// Pehkui
	modImplementation include("com.github.Virtuoel:Pehkui:${project.pehkui_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	// Bewitchment
	modCompileOnly ("curse.maven:bewitchment-394915:${project.bewitchment_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	// Besmirchment
	modCompileOnly ("curse.maven:besmirchment-542196:${project.besmirchment_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	// EnvironmentZ
	modCompileOnly modRuntimeOnly("curse.maven:EnvironmentZ-410224:4000454") {
		exclude group: "net.fabricmc.fabric-api"
		exclude(group: "me.shedaniel.cloth")
	}

	// Dehydration
	modCompileOnly modRuntimeOnly('com.github.Globox1997:Dehydration:1.19-SNAPSHOT') {
		exclude(group: "net.fabricmc.fabric-api")
		exclude(group: "me.shedaniel.cloth")
		exclude(group: "io.github.prospector")
		exclude(group: "curse.maven:motschens-puddles-463169")
	}


}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
